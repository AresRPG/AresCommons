import proguard.gradle.ProGuardTask

import java.util.concurrent.TimeUnit

buildscript {
    repositories(){
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'fr.aresrpg'
version = System.getenv("BUILD_NUMBER") == null ? '0.5' : System.getenv("BUILD_NUMBER");

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

TimeUnit randomizeDictionaryUnit = TimeUnit.DAYS;
long randomizeDictionaryValue = 7;

sourceSets {
    jmh
    shared //Shared between test and jmh
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    testCompile sourceSets.shared.output
    testCompile 'junit:junit:4.12'

    jmhCompile sourceSets.main.output
    jmhCompile sourceSets.shared.output
    jmhCompile 'org.openjdk.jmh:jmh-core:1.12'
    jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
}

task jmh(type:JavaExec) {
    description 'Execute jmh on this project'
    main 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.runtimeClasspath
}

task jmhJar(type: Jar) {
    classifier = "jmh"

    manifest {
        attributes 'Implementation-Title': 'JMH FastReflection Benchmark',
                'Implementation-Version': version,
                'Main-Class': 'org.openjdk.jmh.Main'
    }

    from configurations.jmhCompile.collect {
        it.mkdirs();
        it.isDirectory() ?
                it :
                zipTree(it)
    }

    from sourceSets.jmh.output
}

task shaded(type: Jar) {
    classifier = 'shaded'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task randomizeDictionary(){
    File out = new File("proguard.dictionary.random");
    File inf = new File("proguard.dictionary");
    onlyIf{!out.exists()}
    doLast {
        Random rdm = new Random(((long)(System.currentTimeMillis()/randomizeDictionaryUnit.toMillis(randomizeDictionaryValue)))*485214720/*time/unit*stupid number*/);
        File file = new File("proguard.dictionary");
        List<String> lines = file.readLines();
        def linesMap = [:]
        lines.each { line ->
            linesMap.put(line , rdm.nextInt(Integer.MAX_VALUE))
        };
        lines.sort(new Comparator<String>() {
            @Override
            int compare(String s1, String s2) {
                return Integer.compare(linesMap[s1],linesMap[s2])
            }
        });
        if(out.exists())
            out.setText("");
        else
            out.createNewFile();
        for(String line : lines)
            out.append(line + '\n');
    }
}

task obfuscate(type: ProGuardTask , dependsOn:[jar,randomizeDictionary]){
    configuration 'proguard.conf'

    injars jar.archivePath.getPath()
    outjars jar.destinationDir.getPath() + "/" + jar.baseName + "-obfuscate.jar"
}

idea {
    module {
        scopes.PROVIDED.plus  += [configurations.jmhCompile , configurations.sharedCompile]
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.jmhCompile , configurations.sharedCompile]
    }
}